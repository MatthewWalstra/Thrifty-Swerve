plugins {
  id 'cpp'
  id 'java'
  id 'idea'
  id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
  id 'edu.wpi.first.NativeUtils' version '2021.1.1'
  id 'edu.wpi.first.GradleJni' version '0.10.1'
  id 'edu.wpi.first.GradleVsCode' version '0.11.0'
  id 'edu.wpi.first.GradleRIO' version '2021.2.2'
  id 'google-test-test-suite'
}

allprojects {
    repositories {
        mavenCentral()
    }
    if (project.hasProperty('releaseMode')) {
        wpilibRepositories.addAllReleaseRepositories(it)
    } else {
        wpilibRepositories.addAllDevelopmentRepositories(it)
    }
}

apply from: 'config.gradle'

apply from: 'dependencies.gradle'

nativeUtils {
  exportsConfigs {
      ThriftySwerve {
      }
  }
}

model {
  components {
    ThriftySwerve(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include'
          }
        }
      }

      nativeUtils.useRequiredLibrary(it, "wpilibc_shared", "ntcore_shared", "hal_shared", "wpiutil_shared")
      // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
      wpi.deps.wpilib(it)
      wpi.deps.vendor.cpp(it)
    }
  }

  testSuites {
    ThriftySwerveTest(GoogleTestTestSuiteSpec) {
      testing $.components.ThriftySwerve
      sources.cpp {
        source {
          srcDir 'src/test/cpp'
          include '**/*.cpp'
        }
      }

      nativeUtils.useRequiredLibrary(it, "wpilib_shared", "googletest_static")

      wpi.deps.vendor.cpp(it)
      wpi.deps.wpilib(it)
      wpi.deps.googleTest(it)
    }
  }
  
}

apply from: 'publish.gradle'

wrapper {
  gradleVersion = '6.0'
}
